<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8" />
  <meta name="format-detection" content="telephone=no" />
  <meta name="msapplication-tap-highlight" content="no" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no" />
  <meta name="apple-mobile-web-app-capable" content="yes" />

  <!-- The internet decreed thus: -->
  <!-- https://forum.ionicframework.com/t/whitelist-plugin-does-not-work-external-scripts-fail-to-load-solved/29166/6 -->
  <meta http-equiv="Content-Security-Policy" content="default-src *; script-src data: 'unsafe-inline' 'unsafe-eval' *; style-src 'self' 'unsafe-inline' *">
  <!-- This is a wide open CSP declaration. To lock this down for production, see below. -->
  <!-- Good default declaration:
  * gap: is required only on iOS (when using UIWebView) and is needed for JS->native communication
  * https://ssl.gstatic.com is required only on Android and is needed for TalkBack to function properly
  * Disables use of eval() and inline scripts in order to mitigate risk of XSS vulnerabilities. To change this:
      * Enable inline JS: add 'unsafe-inline' to default-src
      * Enable eval(): add 'unsafe-eval' to default-src
  * Create your own at http://cspisawesome.com
  -->
  <!-- <meta http-equiv="Content-Security-Policy" content="default-src 'self' data: gap: 'unsafe-inline' https://ssl.gstatic.com; style-src 'self' 'unsafe-inline'; media-src *" /> -->

  <title>Language Comparisons</title>
  <link rel="icon" type="image/png" href="/img/favicon.png" />
  <link href="https://fonts.googleapis.com/css?family=Open+Sans" rel="stylesheet">
</head>
<body>

<div id="main">

<p>A quick reference comparison for syntax and features between languages.</p>



<h2>Comments</h2>

<div class="line">
<pre><code class="elm">
-- This is a comment
 </code></pre>

<pre><code class="haskell">
-- This is a comment
 </code></pre>

<pre><code class="roc coffeescript">
# This is a comment
 </code></pre>

</div>





<h2>Primitives</h2>

<div class="line">
<pre><code class="elm">
1 : Int

1.1 : Float

True : Bool

False : Bool

'c' : Char

"hello" : String

-- multi-line String
"""
This is useful for holding JSON or other
content that has "quotation marks".
"""



(1,'c') : (Int, Char)

Just 123 : Maybe Int

Nothing : Maybe a

Ok 123 : Result err Int

Err "Problem!" : Result String a

[ 1, 2, 3 ] : List Int

Array.fromList [ 1, 2, 3] : Array Int



Set.fromList [1, 2, 3] : Set Int


x : Dict Int Bool
x = Dict.fromList [ ( 1 , True ), ( 2, False ) ]


-- Unit type
() : ()

</code></pre>

<pre><code class="haskell">
1 :: Int

1.1 :: Float

True :: Bool

False :: Bool

'c' :: Char

"hello" :: String

-- Requires raw-strings-qq package
{-# LANGUAGE QuasiQuotes #-}
import Text.RawString.QQ
[r|
This is useful for holding JSON or other
content that has "quotation marks".
|]

(1,'c') :: (Int, Char)

Just 123 :: Maybe Int

Nothing :: Maybe a

Right 123 :: Either err Int

Left "Problem!" :: Either String a

[1,2,3] :: [Int] -- Note: type List a = [a]

import qualified Data.Vector as Vector
Vector.fromList [1,2,3] :: Vector Int


import qualified Data.Set as Set
Set.fromList [1,2,3] : Set Int

import qualified Data.Map as Map
x :: Map Int Bool
x = Map.fromList [(1,True), (2, False)]

-- Unit type
() :: ()

</code></pre>

<pre><code class="roc haskell">
1 : I32

1.1 : F64

True : Bool

False : Bool

'c' : U8

"hello" : Str

# multi-line String
"""
This is useful for holding JSON or other
content that has "quotation marks".
"""



# Roc does not have tuple syntax

# or a built-in Maybe type



Ok 123 : Result I32 *

Err "Problem!" : Result * Str

[ 1, 2, 3 ] : List I32

# Roc does not have an Array type,
# but List is implemented using
# a flat array instead of a linked list

[: 1, 2, 3 :] : Set Int


x : Dict I32 Bool
x = {: 1 => True, 2 => False :}


-- Unit type
{} : {}

</code></pre>

</div>


<h2>Custom types</h2>

<p>Note: Custom types are sometimes called tagged unions. Some communities call them ADTs.</p>

<div class="line">
<pre><code class="elm">
type Status = Todo | Doing | Done

Todo : Status

Doing : Status

Done : Status

</code></pre>

<pre><code class="haskell">
data Status = Todo | Doing | Done

Todo :: Status

Doing :: Status

Done :: Status

</code></pre>

<pre><code class="roc haskell">
Status : [ Todo, Doing, Done ]

Todo : Status

Doing : Status

Done : Status

</code></pre>

</div>

<p>Custom types can be polymorphic. <code>Maybe a</code> is an example:</p>

<div class="line">
<pre><code class="elm">
type Maybe a = Just a | Nothing

Just 123 : Maybe Int

Nothing : Maybe a

</code></pre>

<pre><code class="haskell">
data Maybe a = Just a | Nothing

Just 123 :: Maybe Int

Nothing :: Maybe a

</code></pre>

<pre><code class="roc haskell">
Maybe a : [ Just a, Nothing ]

Just 123 : Maybe Int

Nothing : Maybe *

</code></pre>

</div>


<h2>Records</h2>

<div class="line">
<pre><code class="elm">
point =                       -- create a record
    { x = 3, y = 4 }

point.x                       -- access field

List.map .x [ point, { x = 0, y = 0 } ] -- field access function

{ point | x = 6 }             -- update a field

{ point                       -- update many fields
    | x = point.x + 1
    , y = point.y + 1
}


dist { x, y } =               -- pattern matching on fields
    sqrt (x ^ 2 + y ^ 2)

type alias Location =         -- type aliases for records
    { line : Int
    , column : Int
    }

 </code></pre>

<pre><code class="haskell">
-- Haskell has no anonymous records


x point

fmap x [point,point]

point { x = 6 }

point
  { x = (x point) + 1
  , y = (y point) + 1
  }


dist Point{..} = -- Requires RecordWildCards
  sqrt (x^2 + y^2)

data Location =
  Location
    { line : Int
    , column : Int
    }
 </code></pre>

<pre><code class="roc haskell">
point =
    { x: 3, y: 4 }

point.x

List.map [ point, { x: 0, y: 0 } ] .x

{ point & x: 6 }

{
    point &
    x: point.x + 1,
    y: point.y + 1,
}

dist = \{ x, y } ->
    sqrt (x ^ 2 + y ^ 2)

Location :
    {
        line : I32,
        column : I32,
    }
 </code></pre>

</div>



<h2>Functions</h2>

<div class="line">
<pre><code class="elm">
addOne : Int -> Int
addOne x = x + 1

add : Int -> Int -> Int
add x y = x + y

add : Int -> Int -> Int
add = (+)




poly : a -> Int
poly x = 14
 </code></pre>

<pre><code class="haskell">
addOne :: Int -> Int
addOne x = x + 1

add :: Int -> Int -> Int
add x y = x + y

add :: Int -> Int -> Int
add = (+)




poly :: a -> Int
poly x = 14
 </code></pre>

<pre><code class="roc elixir">
addOne : I32 -> I32
addOne = \x -> x + 1

add : I32, I32 -> I32
add = \x, y -> x + y

add : I32, I32 -> I32
add = Num.add
# Roc does not have (+) syntax for operators;
# instead, operators desugar to
# plain function calls like Num.add

poly : * -> I32
poly = \x -> 14
 </code></pre>

</div>



<h2>Anonymous Functions</h2>

<div class="line">
<pre><code class="elm">
square =
    \n -> n ^ 2

squares =
    List.map (\n -> n ^ 2) (List.range 1 100)
 </code></pre>

<pre><code class="haskell">
square =
  \n -> n ^ 2

squares =
  fmap (\n -> n ^ 2) [1..100]
 </code></pre>

<pre><code class="roc elm">
square = \n ->
    n ^ 2

squares =
    List.map (List.range 1 100) \n -> n ^ 2
 </code></pre>

</div>



<h2>Function application & composition</h2>

All the following are equivalent.

<div class="line">
<pre><code class="elm">




viewNames names =
    String.join ", " (List.sort names)

viewNames names =
    String.join ", " <| List.sort names

viewNames names =
    names
        |> List.sort
        |> String.join ", "

viewNames =
    String.join ", " << List.sort

viewNames =
    List.sort >> String.join ", "
 </code></pre>

<pre><code class="haskell">
import qualified Data.List as List
import Data.Function ((&))
import Control.Arrow ((>>>))

viewNames names =
  List.intercalate ", " (List.sort names)

viewNames names =
  List.intercalate ", " $ List.sort names

viewNames names =
  names
    & List.sort
    & (List.intercalate ", ")

viewNames =
  List.intercalate ", " . List.sort

viewNames =
  List.sort . List.intercalate ", "
 </code></pre>

<pre><code class="roc haskell">




viewNames = \names ->
    Str.join (List.sort names Str.alphabetical) ", "

# Roc does not have an equivalent of
# the `<|` operator.

viewNames = \names ->
    names
        |> List.sort Str.alphabetical
        |> Str.join ", "

# Roc does not have an equivalent of
# the `<<` operator

# or the `>>` operator.

 </code></pre>

</div>



<h2>Conditionals</h2>

<div class="line">
<pre><code class="elm">
if power > 9000 then "Wow!" else "Meh"

if key == 40 then
    n + 1

else if key == 38 then
    n - 1

else
    n
 </code></pre>

<pre><code class="haskell">
if power > 9000 then "Wow!" else "Meh"

if key == 40 then
  n + 1

else if key == 38 then
  n - 1

else
  n
 </code></pre>

<pre><code class="roc elm">
if power > 9000 then "Wow!" else "Meh"

if key == 40 then
    n + 1
else if key == 38 then
    n - 1
else
    n



</code></pre>

</div>



<h2>Pattern matching</h2>

<div class="line">
<pre><code class="elm">
case maybe of
    Just xs -> xs
    Nothing -> []

case xs of
    hd::tl -> Just (hd,tl)
    []     -> Nothing

fib n =
    case n of
      0 -> 1
      1 -> 1
      _ -> fib (n-1) + fib (n-2)
 </code></pre>

<pre><code class="haskell">
case maybe of
  Just xs -> xs
  Nothing -> []

case xs of
  hd:tl -> Just (hd,tl)
  []     -> Nothing

fib n =
  case n of
    0 -> 1
    1 -> 1
    _ -> fib (n-1) + fib (n-2)
 </code></pre>

<pre><code class="roc coffeescript">
when maybe is
    Just xs -> xs
    Nothing -> []

# Roc does not have special
# pattern matching syntax for lists


fib = \n ->
    when n is
        0 -> 1
        1 -> 1
        _ -> fib (n - 1) + fib (n - 2)
 </code></pre>

</div>




<h2>Let expressions</h2>

<div class="line">
<pre><code class="elm">
functionWithLetDecs =
    let
        ( three, four ) =
            ( 3, 4 )

        hypotenuse a b =
            sqrt (a^2 + b^2)
    in
    hypotenuse three four

</code></pre>

<pre><code class="haskell">
functionWithLetDecs =
  let
      ( three, four ) =
          ( 3, 4 )

      hypotenuse a b =
          sqrt (a^2 + b^2)
  in
  hypotenuse three four

</code></pre>

<pre><code class="roc haskell">
functionWithLetDecs =
    Pair three four =
        Pair 3 4

    hypotenuse = \a, b ->
        Num.sqrt (a ^ 2 + b ^ 2)

    hypotenuse three four

# No let/in required, indentation sensitive
</code></pre>

</div>




<h2>Modules</h2>

<div class="line">
<pre><code class="elm">
module MyModule exposing (..)

-- qualified imports
import List                            -- List.map, List.foldl
import List as L                       -- L.map, L.foldl

-- open imports
import List exposing (..)              -- map, foldl, concat, ...
import List exposing ( map, foldl )    -- map, foldl

import Maybe exposing ( Maybe )        -- Maybe
import Maybe exposing ( Maybe(..) )    -- Maybe, Just, Nothing



</code></pre>

<pre><code class="haskell">
module MyModule where

-- qualified imports
import qualified List                  -- List.map, List.foldl
import qualified List as L             -- L.map, L.foldl

-- open imports
import List                            -- map, foldl, concat, ...
import List ( map, foldl )             -- map, foldl

import Maybe ( Maybe )        -- Maybe
import Maybe ( Maybe(..) )    -- Maybe, Just, Nothing



</code></pre>

<pre><code class="roc coffeescript">
interface MyModule
    # Roc has no "expose everything" syntax
    exposes [ foo, blah ]
    imports
        [
            # Qualified imports
            List as L, # L.map, L.walk

            # Open imports (Roc has no "expose everything" syntax)
            List.{ map, walk },

            # Imports the Result module and also the Result type
            Result.{ Result },
        ]
 </code></pre>

</div>




<h2>Type Annotations</h2>

<div class="line">
<pre><code class="elm">
answer : Int
answer =
    42

factorial : Int -> Int
factorial n =
    List.product (List.range 1 n)




distance : { x : Float, y : Float } -> Float
distance {x,y} =
    sqrt (x^2 + y^2)
 </code></pre>

<pre><code class="haskell">
answer :: Int
answer =
    42

factorial :: Int -> Int
factorial n =
    List.product (List.range 1 n)

-- Haskell has no anonymous records
data Point =  Point { x :: Float, y :: Float }

distance :: Point -> Float
distance Point{..} = -- Requires RecordWildCards
    sqrt (x^2 + y^2)
 </code></pre>

<pre><code class="roc elm">
answer : I32
answer =
    42

factorial : I32 -> I32
factorial = \n ->
    List.product (List.range 1 n)




distance : { x : F64, y : F64 } -> F64
distance = \{ x, y } ->
    Num.sqrt (x ^ 2 + y ^ 2)
 </code></pre>

</div>




<h2>Type Aliases</h2>

<div class="line">
<pre><code class="elm">
type alias Name = String
type alias Age = Int

info : (Name, Age)
info =
  ("Steve", 28)

type alias Point = { x : Float, y : Float }

origin : Point
origin =
  { x = 0, y = 0 }
 </code></pre>

<pre><code class="haskell">
type Name = String
type Age = Int

info :: (Name, Age)
info =
  ("Steve", 28)

data Point = Point { x :: Float, y :: Float }

origin :: Point
origin =
  Point { x = 0, y = 0 }
 </code></pre>

<pre><code class="roc elm">
Name : Str
Age : I32

info : [ Pair Name Age ]*
info =
    Pair "Steve" 28

Point : { x : F64, y : F64 }

origin : Point
origin =
    { x: 0, y: 0 }
 </code></pre>

</div>


<h2>String interpolation</h2>

In all cases `toEuros` must return a string.

<div class="line">
<pre><code class="elm">
-- None, use concatenation
costOnSale money =
    "It cost " ++ toEuros money ++ " when on sale."













</code></pre>

<pre><code class="haskell">
-- None, use concatenation
costOnSale money =
  "It cost " ++ toEuros money ++ " when on sale."

-- Alternatively, with a QuasiQuotes package
{-# LANGUAGE QuasiQuotes #-}
import NeatInterpolation

costOnSale money =
  let
    euros = toEuros money
  in
  [text|
    It cost ${euros} when on sale.
  |]
 </code></pre>

<pre><code class="roc elm">
costOnSale = \money ->
    euros = toEuros money

    "It cost \(euros) when on sale."












</code></pre>

</div>

  </div>

  <link rel="stylesheet" href="atom-one-dark.min.css">
  <script src="highlight.min.js"></script>
  <script>
    hljs.highlightAll();

    function *$xIter(xp) {
      const iter = document.evaluate( xp, document, null, XPathResult.ORDERED_NODE_ITERATOR_TYPE, null );
      for (;;) { const node = iter.iterateNext(); if (!node) { break; } yield node; }
    }
    setTimeout(function(){
      [...$xIter("//*[contains(text(), '#')]")].map(function(el){
        // Workaround for roc comments when using haskell highlighter
        el.classList.replace("hljs-meta", "hljs-comment")
      })
    }, 100)
  </script>
  <style>
    body, h1, h2, h3, h4, h5, input {
      font-family: 'Open Sans', sans-serif;
    }
    html, body {
      height: 100%;
      width: 1650px;
      margin: 0;
      /* Prevent grey flash in iOS on element selection */
      -webkit-tap-highlight-color: rgba(0,0,0,0) !important;

      padding: 10px;
    }

    #main {
      padding-bottom: 20px;
    }

    .elm::before {
      content: "                                          Elm";
      color: #fff;
    }
    .haskell::before {
      content: "                                      Haskell";
      color: #fff;
    }
    .roc::before {
      content: "                                          Roc";
      color: #fff;
    }

    pre {
      float: left;
      width: 380px;
      margin-right: 8px;
      /* white-space: pre-line; */
    }
    .line code {
      padding-left: 15px !important;
    }

    .line {
      overflow: auto;
    }
  </style>
</body>
</html>
